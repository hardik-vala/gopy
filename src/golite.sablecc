Package golite ;

Helpers
    // All ASCII characters.
    all = [0 .. 127] ;

    // Special whitespace characters.
    tab = 9 ;
    lf = 10 ;
    cr = 13 ;

    // All but End-of-line characters.
    not_eol = [all - [cr + lf]] ;

    // All but '*'.
    not_star = [all - '*'] ;
    // All but '*' and '/'.
    not_star_slash = [all - ['*' + '/']] ;

    // Digit.
    digit = ['0' .. '9'] ;
    // Positive (i.e. non-zero) digit.
    pos_digit = ['1' .. '9'] ;
    // Octal digit.
    oct_digit = ['0' .. '7'] ;
    // Hexidecimal digit.
    hex_digit = digit | ['A' .. 'F'] | ['a' .. 'f'] ;

    // Quotes.
    squote = ''' ;
    dquote = '"' ;
    bquote = '`' ;

    // Escaped characters.
    escapes = '\' ('a' | 'b' | 'f' | 'n' | 'r' | 't' | 'v' | '\') ;
    escape_squote = '\' ''';
    escape_dquote = '\"';

    // Alphabet characters (plus underscore).
    alpha = ['A' .. 'Z'] | ['a' .. 'z'] | '_' ;
    // Alphanumeric characters (plus underscore).
    alpha_num = alpha | digit ;

Tokens
    //* Go keywords. *//
    break = 'break' ;
    case = 'case' ;
    chan = 'chan' ;
    const = 'const' ;
    continue = 'continue' ;
    default = 'default' ;
    defer = 'defer' ;
    else = 'else' ;
    fallthrough = 'fallthrough' ;
    for = 'for' ;
    func = 'func' ;
    go = 'go' ;
    goto = 'goto' ;
    if = 'if' ;
    import = 'import' ;
    interface = 'interface' ;
    map = 'map' ;
    package = 'package' ;
    range = 'range' ;
    return = 'return' ;
    select = 'select' ;
    struct = 'struct' ;
    switch = 'switch' ;
    type = 'type' ;
    var = 'var' ;

    //* GoLite keywords. *//
    int = 'int' ;
    float64 = 'float64' ;
    bool = 'bool' ;
    rune = 'rune' ;
    string = 'string' ;
    print = 'print' ;
    println = 'println' ;
    append = 'append' ;

    //*  Operators. *//
    // Arithmetic.
    plus = '+' ;
    minus = '-' ;
    star = '*' ;
    slash = '/' ;
    perc = '%' ;
    // Bitwise.
    amp = '&' ;
    pipe = '|' ;
    carot = '^';
    lshift = '<<' ;
    rshift = '>>' ;
    amp_carot = '&^' ;
    // Arithmetic operator assignment.
    plus_assign = '+=' ;
    minus_assign = '-=' ;
    star_assign = '*=' ;
    slash_assign = '/=' ;
    perc_assign = '%=' ;
    // Bitwise operator assignment.
    and_assign = '&=' ;
    pipe_assign = '|=' ;
    carot_assign = '^=' ;
    lshift_assign = '<<=' ;
    rshift_assign = '>>=' ;
    amp_carot_assign = '&^=' ;
    // Logical.
    amp_amp = '&&' ;
    pipe_pipe = '||' ;
    not = '!' ;
    // Receive.
    arrow = '<-' ;
    // Increment.
    plus_plus = '++' ;
    // Decrement.
    minus_minus = '--' ;
    // Comparison
    equals = '==' ;
    not_equals = '!=' ;
    lt = '<' ;
    gt = '>' ;
    lte = '<=' ;
    gte = '>=' ;
    // Assignment.
    assign = '=' ;
    short_assign = ':=' ;
    // Ellipsis (Variadic).
    ellipsis = '...' ;
    // Parentheses.
    lparen = '(' ;
    rparen = ')' ;
    // Square brackets.
    lsquare = '[' ;
    rsquare = ']' ;
    // Braces.
    lbrace = '{' ;
    rbrace = '}' ;
    // Other.
    comma = ',' ;
    semi = ';' ;
    dot = '.' ;
    colon = ':' ;

    // Blank space.
    blank = ' ' | tab ;
    // End-of-line.
    eol = lf | cr | cr lf ;

    // Single-line comment and multi-line comments.
    comment = '//' not_eol* | '/*' ([all - '*'] | '*' [all - '/'])* '*/' ;
    // Alternative form for handling multi-line comments:
    // comment = '//' not_eol* | '/*' '*'* [all - '*']+ '*'+ ([all - ['*' + '/']] [all - '*']* '*'+)* '/' ;

    //* Literals. *//

    // Integer.
    int_lit = '0' | pos_digit digit* ;
    oct_lit = '0' oct_digit+ ;
    hex_lit = '0' ['x' + 'X'] hex_digit+ ;

    // Floating-point.
    float_lit = ('0' | pos_digit digit*)? '.' digit* | '.' digit+ ;

    // Rune.
    rune_lit = squote ([not_eol - [squote + '\']] | escape_squote| escapes) squote ;

    // Interpreted string.
    interpreted_string_lit = dquote ([not_eol - [dquote + '\']] | escape_dquote| escapes)* dquote ;

    // Raw string.
    raw_string_lit = bquote [all - bquote]* bquote ;

    //* Identifiers. *//
    id = alpha alpha_num* ;

Ignored Tokens
    blank, eol, comment ;

Productions
    //* Program. *//

    prog {-> prog} = pack_dec top_dec* {-> New prog.prog(pack_dec.id, [top_dec])} ;

    //* Package declaration. *//

    pack_dec {-> id} = package id semi {-> id};

    //* Top-level declaration. *//

    top_dec {-> top_dec} =
          {var_dec} var_dec
            {-> New top_dec.vars([var_dec.var_spec])}
        | {type_dec} type_dec
            {-> New top_dec.types([type_dec.type_spec])}
        | {func_dec} func_dec
            {-> func_dec.top_dec} ;

    //* Variable declaration. *//

    var_dec {-> var_spec*} =
          {sing} var var_spec semi
            {-> [var_spec]}
        | {dist} var lparen var_spec_stmt* rparen semi
            {-> [var_spec_stmt.var_spec]} ;

    var_spec_stmt {-> var_spec} = var_spec semi {-> var_spec} ;

    var_spec {-> var_spec} =
          {typed} id_list type_expr
            {-> New var_spec.spec([id_list.id], type_expr, [])}
        | {init} id_list assign expr_list
            {-> New var_spec.spec([id_list.id], Null, [expr_list.expr])}
        | {typed_init} id_list type_expr assign expr_list
            {-> New var_spec.spec([id_list.id], type_expr, [expr_list.expr])};

    //* Type declaration. *//

    type_dec {-> type_spec*} =
          {sing} type type_spec semi
            {-> [type_spec]}
        | {dist} type lparen type_spec_stmt* rparen semi
            {-> [type_spec_stmt.type_spec]} ;

    type_spec_stmt {-> type_spec} = type_spec semi {-> type_spec} ;

    type_spec {-> type_spec} = id type_expr {-> New type_spec.spec(id, type_expr)} ;

    //* Function declaration. *//

    func_dec {-> top_dec} =
          {with_rettype} func id lparen args rparen type_expr block semi
            {-> New top_dec.func(id, [args.arg_group], type_expr, [block.stmt])}
        | {with_no_rettype} func id lparen args rparen block semi
            {-> New top_dec.func(id, [args.arg_group], Null, [block.stmt])} ;

    args {-> arg_group*} =
          {none} {-> []}
        | {args} arg_before* arg_group {-> [arg_before.arg_group, arg_group]} ;

    arg_before {-> arg_group} = arg_group comma {-> arg_group} ;

    arg_group {-> arg_group} = id_list type_expr {-> New arg_group.arg([id_list.id], type_expr)} ;

    //* Statement. *//

    block {-> stmt*} = lbrace stmt* rbrace {-> [stmt]} ;

    stmt {-> stmt} =
          {simple} simple_sub semi
            {-> simple_sub.stmt}
        | {var_dec} var_dec
            {-> New stmt.var_dec([var_dec.var_spec])}
        | {type_dec} type_dec
            {-> New stmt.type_dec([type_dec.type_spec])}
        | {print} print lparen expr_list rparen semi
            {-> New stmt.print([expr_list.expr])}
        | {println} println lparen expr_list rparen semi
            {-> New stmt.println([expr_list.expr])}
        | {return} return_stmt
            {-> return_stmt.stmt}
        | {if_else} if_else semi
            {-> if_else.stmt}
        | {switch} switcheroo semi
            {-> switcheroo.stmt}
        | {loop} loop semi
            {-> loop.stmt}
        | {break} break semi
            {-> New stmt.break()}
        | {continue} continue semi
            {-> New stmt.continue()} ;

    simple_sub {-> stmt} =
          {expr} expr
            {-> New stmt.expr(expr)}
        | {incr} expr plus_plus
            {-> New stmt.incr(expr)}
        | {decr} expr minus_minus
            {-> New stmt.decr(expr)}
        | {assign} assignment
            {-> assignment.stmt}
        | {short_assign} short_assignment
            {-> short_assignment.stmt} ;

    assignment {-> stmt} =
          {assign} assignable_list assign expr_list
            {-> New stmt.assign([assignable_list.expr], [expr_list.expr])}
        | {plus_assign} assignable plus_assign expr
            {-> New stmt.plus_assign(assignable.expr, expr)}
        | {minus_assign} assignable minus_assign expr
            {-> New stmt.minus_assign(assignable.expr, expr)}
        | {star_assign} assignable star_assign expr
            {-> New stmt.star_assign(assignable.expr, expr)}
        | {slash_assign} assignable slash_assign expr
            {-> New stmt.slash_assign(assignable.expr, expr)}
        | {perc_assign} assignable perc_assign expr
            {-> New stmt.perc_assign(assignable.expr, expr)}
        | {and_assign} assignable and_assign expr
            {-> New stmt.and_assign(assignable.expr, expr)}
        | {pipe_assign} assignable pipe_assign expr
            {-> New stmt.pipe_assign(assignable.expr, expr)}
        | {carot_assign} assignable carot_assign expr
            {-> New stmt.carot_assign(assignable.expr, expr)}
        | {lshift_assign} assignable lshift_assign expr
            {-> New stmt.lshift_assign(assignable.expr, expr)}
        | {rshift_assign} assignable rshift_assign expr
            {-> New stmt.rshift_assign(assignable.expr, expr)}
        | {amp_carot_assign} assignable amp_carot_assign expr
            {-> New stmt.amp_carot_assign(assignable.expr, expr)} ;

    assignable_list {-> expr*} =
          {assignable} assignable
            {-> [assignable.expr]}
        | {assignables} assignable comma assignable_list
            {-> [assignable.expr, assignable_list.expr]} ;

    assignable {-> expr} =
          {id} id
            {-> New expr.variable(id)}
        | {field} field
            {-> field.expr}
        | {array_elem} array_elem
            {-> array_elem.expr} ;

    // Weeding will verify the "dotting" is conducted on a "dottable".
    field {-> expr} = atom dot id {-> New expr.field(atom.expr, id)} ;

    // Weeding will verify the array access is conducted on a "Array accessible".
    array_elem {-> expr} = atom lsquare expr rsquare {-> New expr.array_elem(atom.expr, expr)} ;

    short_assignment {-> stmt} = id_list short_assign expr_list {-> New stmt.short_assign([id_list.id], [expr_list.expr])};

    return_stmt {-> stmt} =
          {void} return semi
            {-> New stmt.return(Null)}
        | {expr} return expr semi
            {-> New stmt.return(expr)} ;

    if_else {-> stmt} =
          {if} if condition block elseif*
            {-> New stmt.if_else(condition, [block.stmt], [elseif], [])}
        | {if_else} if condition [if_block]:block elseif* else [else_block]:block
            {-> New stmt.if_else(condition, [if_block.stmt], [elseif], [else_block.stmt])} ;

    elseif {-> elseif} =
          {elif} else if condition block
            {-> New elseif.elif(condition, [block.stmt])} ;

    condition {-> condition} =
          {expr} expr
            {-> New condition.condition(Null, expr)}
        | {stmt_expr} opt_sub semi expr
            {-> New condition.condition(opt_sub.stmt, expr)} ;

    opt_sub {-> stmt} =
          {empty}
            {-> New stmt.empty()}
        | {sub} simple_sub
            {-> simple_sub.stmt} ;

    switcheroo {-> stmt} =
          {stmt_expr} switch simple_sub semi expr lbrace case_block* rbrace
            {-> New stmt.switch(simple_sub.stmt, expr, [case_block])}
        | {stmt} switch simple_sub semi lbrace case_block* rbrace
            {-> New stmt.switch(simple_sub.stmt, Null, [case_block])}
        | {expr} switch expr lbrace case_block* rbrace
            {-> New stmt.switch(Null, expr, [case_block])}
        | {nothing} switch lbrace case_block* rbrace
            {-> New stmt.switch(Null, Null, [case_block])} ;

    case_block {-> case_block} =
          {case} case expr_list colon stmt*
            {-> New case_block.block(New case_condition.exprs([expr_list.expr]), [stmt])}
        | {default} default colon stmt*
            {-> New case_block.block(New case_condition.default(), [stmt])} ;

    loop {-> stmt} =
          {for} for [init]:opt_sub [first]:semi loop_cond [second]:semi [end]:opt_sub block
            {-> New stmt.for_loop(init.stmt, loop_cond.expr, end.stmt, [block.stmt])}
        | {while_loop} for expr block
            {-> New stmt.while_loop(expr, [block.stmt])}
        | {inf_loop} for block
            {-> New stmt.while_loop(Null, [block.stmt])} ;

    loop_cond {-> expr} =
          {empty}
            {-> New expr.empty()}
        | {cond} expr
            {-> expr} ;

    //* Identifiers *//

    id_list {-> id*} =
          {id} id
            {-> [id]}
        | {ids} id comma id_list
            {-> [id, id_list.id]} ;

    //* Types *//

    type_expr {-> type_expr} =
          {prim_type} prim_type
            {-> prim_type.type_expr}
        | {custom_type} id
            {-> New type_expr.custom(id)}
        | {array} lsquare expr rsquare type_expr
            {-> New type_expr.array(expr.expr, type_expr.type_expr)}
        | {slice} lsquare rsquare type_expr
            {-> New type_expr.slice(type_expr.type_expr)}
        | {struct} struct lbrace struct_sub_stmt* rbrace
            {-> New type_expr.struct([struct_sub_stmt.arg_group])};

    prim_type {-> type_expr} =
          {bool} bool
            {-> New type_expr.bool(bool)}
        | {int} int
            {-> New type_expr.int(int)}
        | {float} float64
            {-> New type_expr.float(float64)}
        | {rune} rune
            {-> New type_expr.rune(rune)}
        | {string} string
            {-> New type_expr.string(string)} ;

    struct_sub_stmt {-> arg_group} = id_list type_expr semi
        {-> New arg_group.arg([id_list.id], type_expr)} ;

    //* Expressions. *//

    expr_list {-> expr*} =
          {empty}
            {-> []}
        | {exprs} expr_before* expr {-> [expr_before.expr, expr]} ;

    expr_before {-> expr} = expr comma {-> expr} ;

    expr {-> expr} =
          {conjunction} conjunction
            {-> conjunction.expr}
        | {or} expr pipe_pipe conjunction
            {-> New expr.or(expr.expr, conjunction.expr)} ;

    conjunction {-> expr} =
          {clause} clause
            {-> clause.expr}
        | {and} conjunction amp_amp clause
            {-> New expr.and(conjunction.expr, clause.expr)} ;

    clause {-> expr} =
          {sub_expr} sub_expr
            {-> sub_expr.expr}
        | {eq} clause equals sub_expr
            {-> New expr.eq(clause.expr, sub_expr.expr)}
        | {neq} clause not_equals sub_expr
            {-> New expr.neq(clause.expr, sub_expr.expr)}
        | {lt} clause lt sub_expr
            {-> New expr.lt(clause.expr, sub_expr.expr)}
        | {lte} clause lte sub_expr
            {-> New expr.lte(clause.expr, sub_expr.expr)}
        | {gt} clause gt sub_expr
            {-> New expr.gt(clause.expr, sub_expr.expr)}
        | {gte} clause gte sub_expr
            {-> New expr.gte(clause.expr, sub_expr.expr)} ;

    sub_expr {-> expr} =
          {factor} factor
            {-> factor.expr}
        | {plus} sub_expr plus factor
            {-> New expr.add(sub_expr.expr, factor.expr)}
        | {minus} sub_expr minus factor
            {-> New expr.subtract(sub_expr.expr, factor.expr)}
        | {bit_or} sub_expr pipe factor
            {-> New expr.bit_or(sub_expr.expr, factor.expr)}
        | {bit_xor} sub_expr carot factor
            {-> New expr.bit_xor(sub_expr.expr, factor.expr)} ;

    factor {-> expr} =
          {term} term
            {-> term.expr}
        | {mult} factor star term
            {-> New expr.mult(factor.expr, term.expr)}
        | {div} factor slash term
            {-> New expr.div(factor.expr, term.expr)}
        | {mod} factor perc term
            {-> New expr.mod(factor.expr, term.expr)}
        | {bit_lshift} factor lshift term
            {-> New expr.bit_lshift(factor.expr, term.expr)}
        | {bit_rshift} factor rshift term
            {-> New expr.bit_rshift(factor.expr, term.expr)}
        | {bit_and} factor amp term
            {-> New expr.bit_and(factor.expr, term.expr)}
        | {bit_clear} factor amp_carot term
            {-> New expr.bit_clear(factor.expr, term.expr)} ;

    term {-> expr} =
          {atom} atom
            {-> atom.expr}
        | {pos} plus atom
            {-> New expr.pos(atom.expr)}
        | {neg} minus atom
            {-> New expr.neg(atom.expr)}
        | {not} not atom
            {-> New expr.not(atom.expr)}
        | {bit_comp} carot atom
            {-> New expr.bit_comp(atom.expr)} ;

    atom {-> expr} =
          {lit} lit
            {-> lit.expr}
        | {id} id
            {-> New expr.variable(id)}
        | {field} field
            {-> field.expr}
        | {func_call} func_call
            {-> func_call.expr}
        | {array_elem} array_elem
            {-> array_elem.expr}
        | {paren} lparen expr rparen
           {-> expr.expr};

    func_call {-> expr} =
          {append} append lparen id comma expr rparen
            {-> New expr.append(id, expr)}
        // Verification of the target type will be done in weeding phase.
        | {type_cast} prim_type lparen expr rparen
            {-> New expr.type_cast(prim_type.type_expr, expr)}
        | {custom_func_call} id lparen expr_list rparen
            {-> New expr.func_call(id, [expr_list.expr])};

    //* Literals. *//

    lit {-> expr} =
          {int_lit} int_lit
            {-> New expr.int_lit(int_lit)}
        | {oct_lit} oct_lit
            {-> New expr.oct_lit(oct_lit)}
        | {hex_lit} hex_lit
            {-> New expr.hex_lit(hex_lit)}
        | {float_lit} float_lit
            {-> New expr.float_lit(float_lit)}
        | {rune_lit} rune_lit
            {-> New expr.rune_lit(rune_lit)}
        | {interpreted_string_lit} interpreted_string_lit
            {-> New expr.interpreted_string_lit(interpreted_string_lit)}
        | {raw_string_lit} raw_string_lit
            {-> New expr.raw_string_lit(raw_string_lit)};

Abstract Syntax Tree
    //* Program. *//

    prog = {prog} id top_dec* ;

    //* Top-level declaration. *//

    top_dec =
          {vars} var_spec*
        | {types} type_spec*
        | {func} id arg_group* type_expr? stmt* ; 

    //* Variable specification. *//

    var_spec = {spec} id+ type_expr? expr* ; 

    //* Type specification. *//

    type_spec = {spec} id type_expr ; 

    //* Argument grouping. *//

    arg_group = {arg} id+ type_expr ; 

    //* Statement *//
    //TODO: Don't forget to typecheck multidimensional arrays/slices
    stmt =
          {empty}
        | {var_dec} var_spec+ 
        | {short_assign} id+ expr+ 
        | {type_dec} type_spec+ 
        | {assign} [lhs]:expr+ [rhs]:expr+
        | {plus_assign} [lhs]: expr [rhs]:expr //TODO: Typecheck this
        | {minus_assign} [lhs]: expr [rhs]:expr 
        | {star_assign} [lhs]: expr [rhs]:expr 
        | {slash_assign} [lhs]: expr [rhs]:expr 
        | {perc_assign} [lhs]: expr [rhs]:expr 
        | {and_assign} [lhs]: expr [rhs]:expr 
        | {pipe_assign} [lhs]: expr [rhs]:expr 
        | {carot_assign} [lhs]: expr [rhs]:expr 
        | {amp_carot_assign} [lhs]: expr [rhs]:expr 
        | {lshift_assign} [lhs]: expr [rhs]:expr 
        | {rshift_assign} [lhs]: expr [rhs]:expr 
        | {incr} expr 
        | {decr} expr 
        | {expr} expr 
        | {print} expr* 
        | {println} expr* 
        | {continue}
        | {break} 
        | {return} expr? 
        | {if_else} condition [if_block]:stmt* [elseif]:elseif* [else_block]:stmt* 
        | {switch} stmt? expr? case_block* 
        | {for_loop} [init]:stmt expr [end]:stmt [block]:stmt* 
        | {while_loop} expr? [block]:stmt* ; 

    elseif = {elif} condition [block]:stmt*;

    condition = {condition} stmt? expr ;

    case_block = {block} case_condition stmt* ;

    case_condition =
          {exprs} expr+
        | {default} ;

    //* Type "expression". *//

    type_expr =
          {bool} bool
        | {int} int
        | {float} float64
        | {rune} rune
        | {string} string
        | {custom} id
        | {array} expr type_expr 
        | {slice} type_expr 
        | {struct} arg_group* ;

    //* Expression. *//

    expr =
          {empty}
        | {add} [left]:expr [right]:expr
        | {subtract} [left]:expr [right]:expr
        | {mult} [left]:expr [right]:expr
        | {div} [left]:expr [right]:expr
        | {mod} [left]:expr [right]:expr
        | {bit_and} [left]:expr [right]:expr
        | {bit_or} [left]:expr [right]:expr
        | {bit_xor} [left]:expr [right]:expr
        | {bit_clear} [left]:expr [right]:expr
        | {bit_lshift} [left]:expr [right]:expr
        | {bit_rshift} [left]:expr [right]:expr
        | {pos} expr
        | {neg} expr
        | {bit_comp} expr
        | {not} expr
        | {eq} [left]:expr [right]:expr
        | {neq} [left]:expr [right]:expr
        | {lt} [left]:expr [right]:expr
        | {lte} [left]:expr [right]:expr
        | {gt} [left]:expr [right]:expr
        | {gte} [left]:expr [right]:expr
        | {and} [left]:expr [right]:expr
        | {or} [left]:expr [right]:expr
        | {func_call} id expr* //TODO: When writing tests for function expressions don't forget to test void types for null pointer errors
        | {append} id expr //TODO: Add weeding pass to check for returns on EVERY path in a function
        | {type_cast} type_expr expr
        | {array_elem} [array]:expr [index]:expr
        | {field} expr id 
        | {variable} id 
        | {int_lit} int_lit
        | {oct_lit} oct_lit
        | {hex_lit} hex_lit
        | {float_lit} float_lit
        | {rune_lit} rune_lit
        | {interpreted_string_lit} interpreted_string_lit
        | {raw_string_lit} raw_string_lit ;
